import org.apache.tools.ant.filters.ReplaceTokens 
 
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
 
group = 'com.example' // Put your groupId here
version = '1.0' // Put your version here
archivesBaseName = 'example' // Put your artifactId here
 
sourceCompatibility = '1.7' // Put the minimum version of Java you want your plugin to run on
targetCompatibility = '1.7' // Put the minimum version of Java you want your plugin to run on
 
def bukkitVersion = '1.8-R0.1-SNAPSHOT' // Put the version of Bukkit/Spigot you want your plugin to depend on
 
repositories {
    jcenter()
 
    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' // Spigot repo
    }
 
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/' // Sonatype snapshots
    }
}
 
dependencies {
    // compile (group: 'org.bukkit', name: 'bukkit', version: bukkitVersion)
    compile (group: 'org.spigotmc', name: 'spigot-api', version: bukkitVersion)
}
 
processResources {
    filter ReplaceTokens, tokens: [
            "VERSION": version
    ]
}
 
compileJava.options.encoding = 'UTF-8' // Encode your source code with UTF-8
 
 
// Generate Sources Jar
task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}
 
// Generate Javadoc Jar
task javadocJar(type: Jar) {
    classifier 'javadoc'
    from javadoc.destinationDir
}
 
artifacts {
    archives sourcesJar, javadocJar // Build Sources and Javadoc Jar
}
